apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.2.4"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'com.jfrog.bintray'

group = 'com.github.bassaer'
version = '1.2.4'

def siteUrl = 'https://github.com/bassaer/ChatMessageView'
def gitUrl = 'https://github.com/bassaer/ChatMessageView.git'
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray_user')
    key = properties.getProperty('bintray_api_key')

    configurations = ['archives']

    pkg {
        repo = 'ChatMessageView'
        name = project.name
        userOrg = 'tnakayama'
        desc = 'Chat UI Library for Android'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/bassaer/ChatMessageView/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['android']
        publicDownloadNumbers = true
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.android.support:support-v4:25.1.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}

def repo = new File(rootDir, "repository")

apply plugin: "maven"

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${repo.absolutePath}"
            pom.version = '1.2.4'
            pom.groupId = 'com.github.bassaer'
            pom.artifactId = 'chatmessageview'
        }
    }
}
